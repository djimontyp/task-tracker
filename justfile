default:
    @just --list

# Aliases
alias ss := services
alias st := services-stop
alias sc := services-clean
alias sca := services-clean-all
alias dbc := db-clear
alias dbs := db-seed
alias dbr := db-reset


# Reinstall UV venv and all deps localy
rds:
    rm -rf .venv
    uv venv --python 3.13
    uv sync --all-groups

# –°–ø–∏—Å–æ–∫ —Å–µ—Ä–≤—ñ—Å—ñ–≤
SERVICES := "postgres nats worker api dashboard nginx"

# Start services (production mode)
services:
    @echo "Starting services (postgres nats worker api dashboard nginx)..."
    docker compose up -d postgres nats worker api dashboard nginx
    @echo "Services started."
    @echo "üåê Available at: http://localhost"
    @echo "üì± Telegram WebApp: http://localhost/webapp"
    @echo "üìä Dashboard: http://localhost/dashboard"
    @echo "üîó Webhook URL: http://localhost/webhook/telegram"

# Start services in development mode with watch
services-dev:
    @echo "Starting services in development mode with file watching..."
    docker compose watch
    @echo "Restarting nginx to refresh connections..."
    docker restart task-tracker-nginx || true

# Start specific service in development mode
dev SERVICE:
    @echo "Starting {{SERVICE}} in development mode..."
    docker compose watch {{SERVICE}}

# Rebuild specific service
rebuild SERVICE:
    @echo "Rebuilding {{SERVICE}}..."
    COMPOSE_BAKE=true docker compose build {{SERVICE}} --no-cache
    docker compose up -d {{SERVICE}}


# Stop services
services-stop:
    @echo "Stopping services..."
    docker compose down postgres nats worker api dashboard nginx
    @echo "Services stopped."

# Full clean services without volumes
services-clean:
    @echo "Stopping and removing services containers..."
    docker compose down
    @echo "Services containers removed."

# Full clean services with images removal
services-clean-all:
    @echo "Stopping and removing services containers and images..."
    docker compose down --rmi all
    @echo "Services containers and images removed."

# Run tests
test:
    @echo "Running tests..."
    uv run python -m pytest

# Run Alembic migrations
alembic-up:
    @echo "Applying database migrations..."
    uv run alembic upgrade head

# Create alembic autogenerated migration
alembic-auto *ARGS:
    @echo "Creating database migration..."
    uv run alembic revision --autogenerate {{ARGS}}

# Lint the code
lint:
    @echo "Linting code..."
    uv run ruff check backend --fix

# Format the code
fmt:
    @echo "Formatting code..."
    uv run ruff format backend

# Run lint and format
check: lint fmt

# Install dependencies
install-dev:
    @echo "Installing dependencies..."
    uv sync --all-groups

# Upgrade dependencies
upgrade:
    @echo "Updating dependencies..."
    uv lock --upgrade --all-groups

# Clear all test data from database
db-clear:
    @echo "Clearing database..."
    uv run python scripts/seed_db.py --clear

# Seed test data into database
db-seed COUNT="50":
    @echo "Seeding {{COUNT}} tasks..."
    uv run python scripts/seed_db.py --seed --count {{COUNT}}

# Clear and seed database (fresh start)
db-reset COUNT="50":
    @echo "Resetting database with {{COUNT}} tasks..."
    uv run python scripts/seed_db.py --clear --seed --count {{COUNT}}
